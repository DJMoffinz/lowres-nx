REM CHARACTER DESIGNER 0.1 BETA
REM BY TIMO KLOSS
REM 2017

DIM PXL(7,7)

REM UI COLORS
PALETTE 0,,%111111,%000011,

REM BIG PIXEL COLORS
PALETTE 1,,%111111,%000000,
PALETTE 2,,%111111,%111111,
PALETTE 3,,%111111,%101010,
PALETTE 4,,%111111,%010101,

REM CHARACTER COLORS
PALETTE 5,,%111111,%101010,%010101

MAIN:

SELCHAR=0
SELPAGE=0
SELCOLOR=1
GOSUB READCHAR
GOSUB READPAGE
GOSUB DRAWUI
WAIT 10

OLDX=-1
OLDY=-1

DO
  IF TOUCH THEN
    X=INT(TOUCH.X/8)
    Y=INT(TOUCH.Y/8)
    IF X<>OLDX OR Y<>OLDY THEN

      IF X>=1 AND X<=8 AND Y>=2 AND Y<=9 THEN
        PXL(X-1,Y-2)=SELCOLOR
        CHAR 64,(SELCOLOR+1,1,,,)
        CELL X,Y
        GOSUB WRITECHAR
      END IF

      IF X>=10 AND X<=11 AND Y>=2 AND Y<=9 THEN
        SELCOLOR=INT((Y-2)/2)
      END IF

      IF X>=1 AND X<=16 AND Y>=11 AND Y<=14 THEN
        SELCHAR=((Y-11)*16)+(X-1)+SELPAGE*64
        GOSUB READCHAR
        GOSUB DRAWCHAR
        GOSUB DRAWCHARNUM
      END IF

      IF X>=17 AND X<=18 THEN
        IF Y>=11 AND Y<=12 AND SELPAGE>0 THEN
          SELPAGE=SELPAGE-1
          GOSUB READPAGE
          GOSUB DRAWCHARNUM
        END IF
        IF Y>=13 AND Y<=14 AND SELPAGE<3 THEN
          SELPAGE=SELPAGE+1
          GOSUB READPAGE
          GOSUB DRAWCHARNUM
        END IF
      END IF

      IF X>=13 AND X<=15 THEN
        IF Y>=2 AND Y<=3 THEN GOSUB BCLEAR
        IF Y>=4 AND Y<=5 THEN GOSUB BCUT
        IF Y>=6 AND Y<=7 THEN GOSUB BCOPY
        IF Y>=8 AND Y<=9 THEN GOSUB BPASTE
      END IF
      IF X>=17 AND X<=19 THEN
        IF Y>=2 AND Y<=3 THEN GOTO BLOAD
        IF Y>=4 AND Y<=5 THEN GOSUB BSAVE
        IF Y>=6 AND Y<=7 THEN GOSUB BFONT
        IF Y>=8 AND Y<=9 THEN GOTO BNEW
      END IF

      OLDX=X
      OLDY=Y
    END IF
  ELSE
    OLDX=-1
  END IF
  WAIT VBL
LOOP

DRAWUI:
CHAR 64,(0,1,,,)
BG FILL 0,0 TO 19,15
FONT (0,1,,,),64
TEXT 1,0,"CHARACTER DESIGNER"
TEXT 13,2,"CLR"
TEXT 13,4,"CUT"
TEXT 13,6,"COP"
TEXT 13,8,"PAS"
TEXT 17,2,"LOA"
TEXT 17,4,"SAV"
TEXT 17,6,"FON"
TEXT 17,8,"NEW"
GOSUB DRAWCHAR
GOSUB DRAWCOLORS
GOSUB DRAWCHARNUM
GOSUB DRAWPAGE
RETURN

DRAWCHAR:
FOR PY=0 TO 7
  FOR PX=0 TO 7
    CHAR 64,(PXL(PX,PY)+1,1,,,)
    CELL PX+1,PY+2
  NEXT PX
NEXT PY
RETURN

DRAWCOLORS:
FOR CI=0 TO 3
  CHAR 64,(CI+1,1,,,)
  BG FILL 10,2+CI*2 TO 11,3+CI*2
NEXT CI
RETURN

DRAWCHARNUM:
FONT (0,1,,,),64
TEXT 1,15,"#"
NUMBER 2,15,SELCHAR,3
NUMBER 14,15,SELPAGE+1,1
TEXT 15,15,"/4"
RETURN

SHOWMESSAGE:
CHAR 64,(0,1,,,)
BG FILL 1,15 TO 18,15
FONT (0,1,,,),64
TEXT 1,15,MSG$
WAIT 30
GOSUB DRAWCHARNUM
RETURN

DRAWPAGE:
CHAR ,(5,0,,,)
CI=192
FOR CY=11 TO 14
  FOR CX=1 TO 16
    CHAR CI,
    CELL CX,CY
    CI=CI+1
  NEXT CX
NEXT CY
FONT (0,1,,,),64
TEXT 18,11,"<"
TEXT 18,13,">"
RETURN

READPAGE:
COPY $A000+SELPAGE*$400,$400 TO $8C00
RETURN

WRITECHAR:
AD=$A000+SELCHAR*16
FOR BI=1 TO 2
  FOR PY=0 TO 7
    V=0
    FOR PX=0 TO 7
      BV=2^(7-PX)
      IF PXL(PX,PY) AND BI THEN V=V OR BV
    NEXT PX
    POKE AD,V
    AD=AD+1
  NEXT PY
NEXT BI
GOSUB CHAR2PAGE
RETURN

CHAR2PAGE:
IF SELCHAR>=SELPAGE*64 AND SELCHAR<(SELPAGE+1)*64 THEN
  COPY $A000+SELCHAR*16,16 TO $8C00+(SELCHAR MOD 64)*16
END IF
RETURN

READCHAR:
AD=$A000+SELCHAR*16
FOR PY=0 TO 7
  FOR PX=0 TO 7
    BV=2^(7-PX)
    PV=0
    IF PEEK(AD) AND BV THEN PV=1
    IF PEEK(AD+8) AND BV THEN PV=PV+2
    PXL(PX,PY)=PV
  NEXT PX
  AD=AD+1
NEXT PY
RETURN

BCLEAR:
FOR PY=0 TO 7
  FOR PX=0 TO 7
    PXL(PX,PY)=SELCOLOR
  NEXT PX
NEXT PY
GOSUB WRITECHAR
GOSUB DRAWCHAR
GOSUB CHAR2PAGE
RETURN

BCUT:
AD=$A000+SELCHAR*16
COPY AD,16 TO $B000
FILL AD,16,0
GOSUB READCHAR
GOSUB DRAWCHAR
GOSUB CHAR2PAGE
RETURN

BCOPY:
AD=$A000+SELCHAR*16
COPY AD,16 TO $B000
RETURN

BPASTE:
AD=$A000+SELCHAR*16
COPY $B000,16 TO AD
GOSUB READCHAR
GOSUB DRAWCHAR
GOSUB CHAR2PAGE
RETURN

BLOAD:
LOAD "#0",$A000
GOTO MAIN

BSAVE:
SAVE "#0:CHAR DEFAULT",$A000,$1000
MSG$="SAVED"
GOSUB SHOWMESSAGE
RETURN

BFONT:
COPY $E000,$400 TO $A000+SELPAGE*$400
GOSUB READCHAR
GOSUB READPAGE
GOSUB DRAWCHAR
GOSUB DRAWPAGE
RETURN

BNEW:
FILL $A000,$1000,0
GOTO MAIN
