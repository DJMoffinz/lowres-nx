REM BACKGROUND DESIGNER 0.1 BETA
REM BY TIMO KLOSS
REM 2017

REM UI COLORS
PALETTE 7,,%111111,%000011,

REM CHARACTER COLORS
PALETTE 0,,%111111,%101010,%010101

SELCHAR=1
SELPAGE=0
SELW=0
SELH=0
EDMODE=0
MAPX=0
MAPY=0
DRAGX=0
DRAGY=0

REM ======================

NEWMENU:
CHAR 64,(7,1,,,)
BG FILL 0,0 TO 19,15
FONT (7,1,,,),64
TEXT 0,0,"BACKGROUND DESIGNER"
TEXT 1,3,"NEW 20*16"
TEXT 1,5,"NEW 32*16"
TEXT 1,7,"NEW 32*32"
TEXT 1,9,"LOAD"
DO
  IF TOUCH THEN
    Y=INT(TOUCH.Y/8)
    IF Y>=3 AND Y<=4 THEN
      MAPW=20
      MAPH=16
      GOTO BNEW
    END IF
    IF Y>=5 AND Y<=6 THEN
      MAPW=32
      MAPH=16
      GOTO BNEW
    END IF
    IF Y>=7 AND Y<=8 THEN
      MAPW=32
      MAPH=32
      GOTO BNEW
    END IF
    IF Y>=9 AND Y<=10 THEN GOTO BLOADBG
  END IF
  WAIT VBL
LOOP

BNEW:
FILL $B000,2+MAPW*MAPH*2,0
GOTO CHARMENU

REM ======================

MAPEDITOR:
COPY $A000,$1000 TO $8000
GOSUB DRAWMAP
GOSUB DRAWTOOLBAR
WAIT 10

OLDX=-1
OLDY=-1

DO
  IF TOUCH THEN
    X=INT(TOUCH.X/8)
    Y=INT(TOUCH.Y/8)
    IF X<>OLDX OR Y<>OLDY THEN

      IF Y>=14 THEN
        IF X>=0 AND X<=1 THEN EDMODE=0
        IF X>=2 AND X<=3 THEN EDMODE=1
        IF X>=18 AND X<=19 THEN GOTO CHARMENU
      ELSE
        IF EDMODE=0 THEN
          CHAR ,(0,0,0,0,0)
          W=MIN(SELW,19-X)
          H=MIN(SELH,13-Y)
          FOR I=0 TO H
            FOR J=0 TO W
              C=SELCHAR+I*16+J
              CHAR C,
              CELL X+J,Y+I
              POKE $B002+((Y+I+MAPY)*MAPW+(X+J+MAPX))*2,C
            NEXT J
          NEXT I
        ELSE IF EDMODE=1 THEN
          IF OLDX>=0 THEN
            MAPX=MIN(MAX(MAPX-X+DRAGX,0),MAPW-20)
            MAPY=MIN(MAX(MAPY-Y+DRAGY,0),MAPH-14)
            GOSUB DRAWMAP
          END IF
          DRAGX=X
          DRAGY=Y
        END IF
      END IF

      OLDX=X
      OLDY=Y
    END IF
  ELSE
    OLDX=-1
  END IF
  WAIT VBL
LOOP

DRAWMAP:
BG SOURCE $B002,MAPW
BG COPY MAPX,MAPY,20,14 TO 0,0
RETURN

DRAWTOOLBAR:
CHAR 64,(7,1,,,)
BG FILL 0,14 TO 19,15
FONT (7,1,,,),64
TEXT 0,14,"D"
TEXT 2,14,"M"
TEXT 18,14,"ME"
TEXT 18,15,"NU"
RETURN

REM ======================

CHARMENU:
GOSUB READPAGE
GOSUB DRAWUI
WAIT 10

OLDX=-1
OLDY=-1

DO
  IF TOUCH THEN
    X=INT(TOUCH.X/8)
    Y=INT(TOUCH.Y/8)
    IF X<>OLDX OR Y<>OLDY THEN

      IF Y>=14 AND Y<=15 THEN
        IF X>=18 AND X<=19 THEN GOTO MAPEDITOR
      END IF

      IF X>=1 AND X<=16 AND Y>=1 AND Y<=4 THEN
        IF OLDX=-1 THEN
          SELCHAR=((Y-1)*16)+(X-1)+SELPAGE*64
          SELW=0
          SELH=0
          GOSUB DRAWCHARNUM
          DRAGX=X
          DRAGY=Y
        ELSE
          SELW=MAX(X-DRAGX,0)
          SELH=MAX(Y-DRAGY,0)
          GOSUB DRAWCHARNUM
        END IF
      END IF

      IF X>=17 AND X<=18 THEN
        IF Y>=1 AND Y<=2 AND SELPAGE>0 THEN
          SELPAGE=SELPAGE-1
          GOSUB READPAGE
          GOSUB DRAWCHARNUM
        END IF
        IF Y>=3 AND Y<=4 AND SELPAGE<3 THEN
          SELPAGE=SELPAGE+1
          GOSUB READPAGE
          GOSUB DRAWCHARNUM
        END IF
      END IF

      IF X>=1 AND X<=8 THEN
        IF Y>=7 AND Y<=8 THEN GOSUB BLOADCHR
        IF Y>=9 AND Y<=10 THEN GOTO BLOADBG
        IF Y>=11 AND Y<=12 THEN GOSUB BSAVEBG
        IF Y>=13 AND Y<=14 THEN GOTO NEWMENU
      END IF

      OLDX=X
      OLDY=Y
    END IF
  ELSE
    OLDX=-1
  END IF
  WAIT VBL
LOOP

DRAWUI:
CHAR 64,(7,1,,,)
BG FILL 0,0 TO 19,15
FONT (7,1,,,),64
TEXT 1,7,"LOAD(CH)"
TEXT 1,9,"LOAD(BG)"
TEXT 1,11,"SAVE(BG)"
TEXT 1,13,"NEW"
TEXT 18,14,"ED"
TEXT 18,15,"IT"
GOSUB DRAWCHARNUM
GOSUB DRAWPAGE
RETURN

DRAWCHARNUM:
FONT (7,1,,,),64
TEXT 1,5,"#"
NUMBER 2,5,SELCHAR,3
NUMBER 6,5,SELW+1,1
TEXT 7,5,"*"
NUMBER 8,5,SELH+1,1
NUMBER 14,5,SELPAGE+1,1
TEXT 15,5,"/4"
RETURN

DRAWPAGE:
CHAR ,(0,0,,,)
CI=192
FOR CY=1 TO 4
  FOR CX=1 TO 16
    CHAR CI,
    CELL CX,CY
    CI=CI+1
  NEXT CX
NEXT CY
FONT (7,1,,,),64
TEXT 18,1,"<"
TEXT 18,3,">"
RETURN

READPAGE:
COPY $A000+SELPAGE*$400,$400 TO $8C00
RETURN

BLOADBG:
LOAD "#1",$B000
MAPW=PEEK($B000)
MAPH=PEEK($B001)
GOTO MAPEDITOR

BSAVEBG:
POKE $B000,MAPW
POKE $B001,MAPH
SAVE "#1:BG DEFAULT",$B000,2+MAPW*MAPH*2
RETURN

BLOADCHR:
LOAD "#0",$A000
GOSUB READPAGE
GOSUB DRAWPAGE
RETURN
